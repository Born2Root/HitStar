; ######################################
;
; ************************************
; nanDECK playing card generator Script
; ************************************
;
; Purpose: Generate the actual HitStar playing cards
;
; Usage:
;   - provide an Excel-file containing the whole dataset for the cards (change path below in the code)
;   - open the script in nanDECK
;   - click the "Validate deck" button to check if everything is correct
;   - clickthe "Build deck" button to generate the cards
;   - save the result by clicking the "PDF" or "Print deck" button
;
; Used Tool Version: nanDECK v1.28.1
;
; ######################################

;disable Unicode conversion. Otherwise special characters like ö,ä,è are not read in correctly from the Excel file
LINKUNI=OFF
;path to the Excel-data file, and defining the tablename that should be used from the Excel. In this example the worksheet with the name "print"
LINK="C:\Projects\Hitstar\Hitstar.xlsx!print"

;define cm as main measurement unit
UNIT = cm
;H = the cards are horizontally centered & V = the cards are vertically centered & O = guides are not printed on odd pages
PAGE = 21, 29.7 , PORTRAIT, HV
;just cutting marks as border
border = mark, , 0.01,mark
;define card size = 5cm x 5cm
CARDSIZE = 5, 5

;create two ranges for the fronts & backs of the cards
;the uses the coloumn "Interpret" to calculate how many lines are used in the table and therefore how big the range should be
[range_front]=1-{(Interpret)}
[range_back]={(Interpret)+1}-{(Interpret)*2}

VISUAL=, 10, 10

    FONT=arial,8,,#000000
    HTMLFONT=Year,Arial,35,B,#000000,CENTER
    HTMLTEXT=[range_front],[Jahr],5%,30%,94%,32%,#FFFFFF,0,B,100,Year,Year

    HTMLFONT=Song,Arial,12,,#000000,CENTER
    HTMLTEXT=[range_front],[Interpret],3%,10%,93%,34%,#FFFFFF,0,B,100,Song,Song
    HTMLTEXT=[range_front],[Titel],3.5%,70%,94%,30%,#FFFFFF,0,B,100,Song,Song
    IMAGE=[range_front],[Icon],85%,5%,10%,10%,0,PN

    ;use the QR-Code images for the backs
    IMAGE=[range_back],[QR],15%,15%,70%,70%
    ;the nanDECK integrated QR-Generator is not useful as the generated QR-Codes are not recognized by the MIT-App Inventor QR-Code Reader that is used in the mobile App
    ;QRCODE="1-{(LINK)}",[LINK],{739/40}%,{16199/600}%,{24203/400}%,{1999/50}%

ENDVISUAL

;print the cards in duplex mode - adjusted fronts to backs
duplex=[range_front],[range_back]
print=duplex

;For example, if you have a deck where cards 1-50 are the fronts, and 51-100 are the backs, you can print them correctly paired by adding these lines:
;DUPLEX=1-50,51-100
;PRINT=DUPLEX

;And if cards 1-50 are the fronts, and 51 is the back for all of them:
;DUPLEX=1-50,51
;PRINT=DUPLEX
